group 'com.hart'
version '1.1.0'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: "com.github.samueltbrown.cucumber"

dependencies {
	// JUNIT
	testCompile group: 'junit', name: 'junit', version: '4.12'

	// ASSERTJ
	testCompile 'org.assertj:assertj-core:3.5.2'

	// CUCUMBER
	testCompile 'info.cukes:cucumber-java:1.2.4'
	testCompile 'info.cukes:cucumber-junit:1.2.4'

	// PICOCONTAINER
	compile group: 'org.picocontainer', name: 'picocontainer', version: '2.15'
	compile group: 'info.cukes', name: 'cucumber-picocontainer', version: '1.2.4'

	// UNIREST
	compile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
	compile group: 'commons-io', name: 'commons-io', version: '2.5'

	// GSON
	compile group: 'com.google.code.gson', name: 'gson', version: '2.7'

	// LOG4J
	compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7'
	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
}

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath "com.github.samueltbrown:gradle-cucumber-plugin:0.9"
		classpath "net.masterthought:cucumber-reporting:0.4.0"
	}
}


test {
	systemProperty "CLARITY_USER_EMAIL", System.getProperty('CLARITY_USER_EMAIL')
	systemProperty "CLARITY_USER_PASSWORD", System.getProperty('CLARITY_USER_PASSWORD')

	testLogging {
		// add output for all test statuses (but not "started")
		events "passed", "skipped", "failed", "standardOut", "standardError"

		// print stdout, stderr to console
		testLogging.showStandardStreams = true

		// output exceptions including assertion failure reasons
		showExceptions = true
		showCauses = true
		showStackTraces = false
		exceptionFormat = 'full'
	}

	afterTest { desc, result ->
		println "TEST ${desc.name} [${desc.className}] result: ${result.resultType}"
	}
}

cucumber {
	formats = ['pretty', 'html:target/cucumber', 'json:target/cucumber.json']
	featureDirs = ['src/cucumber/resources/features']
	glueDirs = ['clarity.api.hooks', 'clarity.api.steps']

	def CUCUMBER_TAGS = System.getProperty('CUCUMBER_TAGS') ?: ''
	println "CUCUMBER_TAGS: " + CUCUMBER_TAGS
	tags = [ CUCUMBER_TAGS ]
//    tags = ['@Login, @PatientSearch, @Users, @Demographics, @Charts']

	monochrome = false
	strict = false
	dryRun = false
	ignoreFailures = false

	jvmOptions {
//        maxHeapSize = '512m'
	}
}

